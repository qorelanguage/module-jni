#!/usr/bin/env qore
# -*- mode: qore; indent-tabs-mode: nil -*-

%require-types
%enable-all-warnings
%new-style
%strict-args
%allow-injection

%requires Util
%requires QUnit
%requires ../qlib/MqttDataProvider

%exec-class MqttDataProviderTest

public class MqttDataProviderTest inherits QUnit::Test {
    private {
        MqttDataProvider mqtt;

        # command-line options
        const MyOpts = Opts + {
            "conn": "c,connection=s",
        };

        const OptionColumn = 22;
    }

    constructor() : Test("MqttDataProviderTest Test", "1.0", \ARGV, MyOpts) {
        addTestCase("MQTT test", \mqttTest());

        try {
            setupConnection();
        } catch (hash<ExceptionInfo> ex) {
            if (m_options.verbose > 2) {
                printf("%s\n", get_exception_string(ex));
            } else if (m_options.verbose) {
                printf("%s: %s\n", ex.err, ex.desc);
            }
        }

        # Return for compatibility with test harness that checks return value.
        set_return_value(main());
    }

    mqttTest() {
        if (!mqtt) {
            testSkip("no connection");
        }

        assertEq("mqtt", mqtt.getName());
    }

    private usageIntern() {
        TestReporter::usageIntern(OptionColumn);
        printOption("-c,--connection=ARG", "set connection name", OptionColumn);
    }

    private error(string fmt) {
        throw "OPTION-ERROR", sprintf("%s: ERROR: %s", get_script_name(), vsprintf(fmt, argv));
    }

    private setupConnection() {
        *string conn_name = m_options.conn ?? ENV.MQTT_CONNECTION;
        if (!conn_name.val()) {
            error("missing -c,--connection option or MQTT_CONNECTION environment variable");
        }

        AbstractConnection conn = get_connection(conn_name);
        if (!(conn instanceof MqttConnection)) {
            error("connection %y is type %y; expecting \"MqttConnection\"", conn_name, conn.className());
        }
        mqtt = conn.getDataProvider();
    }
}
