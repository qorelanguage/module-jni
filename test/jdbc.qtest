#!/usr/bin/env qore

%new-style
%require-types
%strict-args
%enable-all-warnings

%requires jni
%requires QUnit

%exec-class Main

public class Main inherits QUnit::Test {
    private {
        const MyOpts = Opts + {
            "connstr": "c,conn=s",
        };

        const OptionColumn = 22;
    }

    constructor() : Test("jdbc test", "1.0", \ARGV, MyOpts) {
        addTestCase("pgsqlTest", \pgsqlTest());
        addTestCase("oracleTest", \oracleTest());
        addTestCase("firebirdTest", \firebirdTest());

        # execute tests and set program return value
        set_return_value(main());
    }

    private oracleTest() {
        *AbstractDatasource ds = getConnection("QORE_DB_CONNSTR_JDBC_ORACLE");
        if (!ds) {
            testSkip("no jdbc connection available");
        }

        auto v = ds.getServerVersion();
        assertTrue(exists v);
        v = ds.getClientVersion();
        assertTrue(exists v);
        assertEq("Oracle", ds.getDriverRealName());

        # create table
        ds.exec("create table jdbc_test (input_1 number(14), input_2 varchar2(20))");
        on_error ds.rollback();
        on_success ds.commit();
        on_exit ds.exec("drop table jdbc_test");

        doTestIntern(ds);
    }

    private pgsqlTest() {
        *AbstractDatasource ds = getConnection("QORE_DB_CONNSTR_JDBC_PGSQL");
        if (!ds) {
            testSkip("no jdbc connection available");
        }

        auto v = ds.getServerVersion();
        assertTrue(exists v);
        v = ds.getClientVersion();
        assertTrue(exists v);
        assertEq("PostgreSQL", ds.getDriverRealName());

        # create table
        ds.exec("create table jdbc_test (input_1 numeric(14), input_2 varchar(20))");
        on_error ds.rollback();
        on_success ds.commit();
        on_exit ds.exec("drop table jdbc_test");

        doTestIntern(ds);
    }

    private firebirdTest() {
        *AbstractDatasource ds = getConnection("QORE_DB_CONNSTR_JDBC_FIREBIRD");
        if (!ds) {
            testSkip("no jdbc connection available");
        }

        auto v = ds.getServerVersion();
        assertTrue(exists v);
        v = ds.getClientVersion();
        assertTrue(exists v);
        assertRegex("^Firebird", ds.getDriverRealName());

        # create table
        ds.exec("create table jdbc_test (input_1 numeric(14), input_2 varchar(20))");
        ds.commit();
        on_error ds.rollback();
        on_success ds.commit();
        on_exit ds.exec("drop table jdbc_test");

        doTestIntern(ds);
    }

    private doTestIntern(AbstractDatasource ds) {
        # insert a row with all null values
        int rows_affected = ds.exec("insert into jdbc_test (input_1, input_2) values (%v, %v)");
        assertEq(1, rows_affected);

        hash<auto> q = ds.selectRow("select * from jdbc_test");
        assertEq({"input_1": NULL, "input_2": NULL}, q);

        rows_affected = ds.exec("delete from jdbc_test");
        assertEq(1, rows_affected);

        rows_affected = ds.exec("insert into jdbc_test (input_1, input_2) values (%v, %v)", 1, "test");
        q = ds.selectRow("select * from jdbc_test");
        assertEq({"input_1": 1, "input_2": "test"}, q);

        q = ds.select("select * from jdbc_test");
        assertEq({"input_1": (1,), "input_2": ("test",)}, q);

        list<auto> rows = ds.selectRows("select * from jdbc_test");
        assertEq(({"input_1": 1, "input_2": "test"},), rows);

        rows_affected = ds.exec("delete from jdbc_test where input_1 = %v", 1);
        assertEq(1, rows_affected);
    }

    private usageIntern() {
        TestReporter::usageIntern(OptionColumn);
        printOption("-c,--conn=ARG", "set DB connection argument (ex: \"jdbc:user/pass@driver?...\")", OptionColumn);
    }

    private *AbstractDatasource getConnection(string var) {
        try {
            if (!ENV{var}.val()) {
                throw "ENV-VAR-ERROR", sprintf("envionment var %y for connection is not set", var);
            }
            Datasource ds(ENV{var});
            if (ds.getDriverName() != "jdbc") {
                stderr.printf("cannot execute jdbc tests on a connection using driver %y (var: %y = %y)\n",
                    ds.getDriverName(), var, ENV{var});
            }
            ds.open();
            return ds;
        } catch (hash<ExceptionInfo> ex) {
            printf("ERROR: %s: %s\n", ex.err, ex.desc);
        }
    }
}